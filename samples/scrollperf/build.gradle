/*
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

import com.facebook.fresco.buildsrc.TestDeps

apply plugin: 'com.android.application'

android {
    compileSdkVersion FrescoConfig.compileSdkVersion
    buildToolsVersion FrescoConfig.buildToolsVersion

    defaultConfig {
        applicationId "com.facebook.samples.scrollperf"
        minSdkVersion FrescoConfig.samplesMinSdkVersion
        targetSdkVersion FrescoConfig.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-scrollperf.pro'
        }
    }
    flavorDimensions "useSoLoader"
    productFlavors {
        soLoader {
        }
        noSoLoader {
        }
        noNativeCode {
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    sourceSets {
        main {
            res.srcDirs = ['src/main/res', 'src/androidTest/res']
        }
        androidTest {
            res.srcDirs = ['src/androidTest/res']
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:${versions.androidx.appcompat}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidx.recyclerview}"
    implementation "androidx.preference:preference:${versions.androidx.preference}"

    implementation (fileTree(dir: 'libs', include: ['*.jar']))

    noSoLoaderImplementation (project(':static-webp')) {
        exclude group: 'com.facebook.soloader', module: 'soloader'
        exclude module: 'soloader'
    }
    noSoLoaderImplementation (project(':drawee-backends:drawee-pipeline')) {
        exclude group: 'com.facebook.soloader', module: 'soloader'
        exclude module: 'soloader'
    }

    noNativeCodeImplementation (project(':static-webp')) {
        exclude group: 'com.facebook.soloader', module: 'soloader'
        exclude module: 'native-filters'
        exclude module: 'imagepipeline-native'
        exclude module: 'nativememory'
        exclude module: 'native-imagetranscoder'
    }
    noNativeCodeImplementation (project(':drawee-backends:drawee-pipeline')) {
        exclude group: 'com.facebook.soloader', module: 'soloader'
        exclude module: 'native-filters'
        exclude module: 'imagepipeline-native'
        exclude module: 'nativememory'
        exclude module: 'native-imagetranscoder'
    }

    soLoaderImplementation project(':static-webp')
    soLoaderImplementation project(':drawee-backends:drawee-pipeline')

    testImplementation TestDeps.junit

    androidTestImplementation TestDeps.junit
    androidTestImplementation "org.mockito:mockito-core:${MOCKITO_CORE_VERSION}"
    androidTestImplementation("org.powermock:powermock-api-mockito2:${POWERMOCK_VERSION}") {
        exclude group: 'org.mockito', module: 'mockito-all'
    }
    androidTestImplementation "org.powermock:powermock-module-junit4:${POWERMOCK_VERSION}"
    androidTestImplementation "org.powermock:powermock-module-junit4-rule:${POWERMOCK_VERSION}"
    androidTestImplementation "org.powermock:powermock-classloading-xstream:${POWERMOCK_VERSION}"
    androidTestImplementation("org.robolectric:robolectric:${ROBOLECTRIC_VERSION}") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}
